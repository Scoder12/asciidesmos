Program = _{ SOI ~ Expression ~ EOF }

ExpressionNoList =  { UnaryExpression | BinaryExpression | Term }
Expression = { List | UnaryExpression | BinaryExpression | Term }
Term = _{ Call | Variable | Number | "(" ~ Expression ~ ")" }

Factorial = @{ "!" }
UnaryOperator = @{ Factorial }
UnaryExpression = { Term ~ UnaryOperator }

Add =      @{ "+" }
Subtract = @{ "-" }
Multiply = @{ "*" }
Divide =   @{ "/" }
BinaryOperator = @{ Add | Subtract | Multiply | Divide }
BinaryExpression = { Term ~ (BinaryOperator ~ Term)+ }

PositiveSign = @{ "+" }
NegativeSign = @{ "-" }
NumberSign = @{ PositiveSign | NegativeSign }

DecimalPoint = @{ "." }
Number = @{ NumberSign? ~ ASCII_DIGIT+ ~ (DecimalPoint ~ ASCII_DIGIT+)? }

Letter = { 'a'..'z' | 'A'..'Z' }
Identifier = @{ Letter ~ (Letter | ASCII_DIGIT)* }

Variable = @{ Identifier }

// Trailing commas ("a(1,)") won't parse
Arguments = { Expression ~ ("," ~ Expression)* }
ArgumentsNoList = { ExpressionNoList ~ ("," ~ ExpressionNoList)*  }

// Nested lists are not allowed
List = { "[" ~ ArgumentsNoList? ~ "]" }

Call = { Identifier ~ "(" ~ Arguments? ~ ")" }

WHITESPACE = _{ " " | "\t" }

EOF = _{ EOI | ";" }
